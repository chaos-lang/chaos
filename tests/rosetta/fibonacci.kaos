// Fibonacci number generators


// Recursion

num def fibo_error()
    print "Incorrect input"
    num r = 0
    return r
end

num def sum(num j)
    num x = fibo1(j - 1)
    num y = fibo1(j - 2)
    num z = x + y
    return z
end

num def fibo1(num n)
    num zero = 0
    num one = 1
    num c = n
end {
    n < 0   : fibo_error(),
    n == 0  : return zero,
    n == 1  : return one,
    default : sum(c)
}

print fibo1(-1) // Incorrect input

num x = 1
12 times do
    print fibo1(x)
    x++
end
del x


// Dynamic programming

import array

num def fibo2(num n)
    num list f = [0, 1]

    num i = 2
    num a = 0
    num b = 0
    n - 1 times do
        a = f[i - 1]
        b = f[i - 2]
        f = array.insert(f, a + b)
        i++
    end

    num r = f[n]
    return r
end


num x = 1
20 times do
    print fibo2(x)
    x++
end
del x


// Space optimized method

num def fibo3(num n)
    num a = 0
    num b = 1
    num c = 0
    n - 1 times do
        c = a + b
        a = b
        b = c
    end
end {
    n == 0  : return a,
    n == 1  : return b,
    default : return b
}


num x = 1
20 times do
    print fibo3(x)
    x++
end
